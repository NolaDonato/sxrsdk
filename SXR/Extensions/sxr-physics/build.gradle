apply plugin: 'com.android.library'

repositories {
    flatDir {
        dirs '../../sxr-libs/'
    }
}

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26

        externalNativeBuild {
            cmake {
                arguments.add('-DANDROID_STL=c++_shared')
            }
        }
    }

    packagingOptions {
        pickFirst 'lib/x86/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
    }

    buildTypes {
        debug {
            debuggable = true
            jniDebuggable = true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    sourceSets.main {
        java.srcDirs = ['src/main/java']
        jni.srcDirs = [ ] // no auto generation of Android.mk
        jniLibs.srcDirs - [ 'src/main/jniLibs' ]
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
}

project.ext.jomlVersion = "1.9.3-SNAPSHOT"

dependencies {
    implementation "org.joml:joml-android:${jomlVersion}"
    if (findProject(':sxrsdk')) {
        implementation project (':sxrsdk')
    } else {
        implementation(name: 'sxrsdk-debug', ext: 'aar')
    }
//    implementation fileTree(dir: 'src/main/jni/aar', include: '*.aar' )
}

clean {}.doLast {
    def sxrLibs = project.rootDir.absolutePath + "/../sxr-libs"
    if (file(sxrLibs).exists()) {
        project.delete(sxrLibs + '/' + project.name + '-release.aar')
        project.delete(sxrLibs + '/' + project.name + '-debug.aar')
    }
}

task extractSo (type: Copy) {
    println 'Extracting *.so file(s)....'

    from zipTree("src/main/aar/bullet3-debug.aar")
    into "src/main/jniLibs/"
    include "jni/**/*.so"

    eachFile {
        def segments = it.getRelativePath().getSegments() as List
        println segments
        it.setPath(segments.tail().join("/"))
        return it
    }
    includeEmptyDirs = false
}

gradle.projectsEvaluated {
    preBuild.dependsOn(extractSo)
}

task copyJarToBin(type: Copy) {
    outputs.upToDateWhen { false }
    from tasks.matching { it.name.startsWith('bundle') }
    into project.rootDir.absolutePath + "/../sxr-libs"
}

tasks.whenTaskAdded { task ->
    if (task.name == 'bundleDebugAar') {
        task.finalizedBy(copyJarToBin)
    } else if (task.name == 'bundleReleaseAar') {
        task.finalizedBy(copyJarToBin)
    }
}
